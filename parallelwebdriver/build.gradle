apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
group = 'selenium.webdriver.examples'

// This project will work with the --offline arg if you don't have internet access.
// The initial run (of course) requires internet to download the libs.

ext {
    projTitle = 'Test Parallel MultiWindow'
    projVersion = '1.0'
}

dependencies {
    compile project(':commonlib')
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'org.seleniumhq.selenium', name: 'selenium-java', version: '2.+'
    compile group: 'commons-collections', name: 'commons-collections', version: '3.+'
    compile group: 'junit', name: 'junit', version: '4.+'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.+'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.+'
}

// running the task distZip creates a .zip archive of this project.
// to run it, unzip it and run the embedded bin/project.bat script.

task show << {
    println ""
    println "-----------------------------------------------"
    println "Project Name: " + projTitle
    println "Version: " + projVersion
    println "Home directory: "
    println "Build output: " + relativePath(compileJava.destinationDir)
    println "Resources output: " + relativePath(processResources.destinationDir)
    println "Test output dir: " + relativePath(sourceSets.test.output.classesDir)
    println "Test result dir: " + relativePath(testResultsDir)
    println "Report dir: " + relativePath(testReportDir)
    println "-----------------------------------------------"
} 

tasks.withType(Test) {

    jvmArgs '-Xms128m', '-Xmx1024m', '-XX:MaxPermSize=128m'
    maxParallelForks = 4
	
	// System properties passed to tests (if not http://localhost:8001/index.html)
	systemProperties['testProtocol'] = 'http'
	systemProperties['testDomain'] = 'djangofan.github.com'
	systemProperties['testPort'] = 80
	systemProperties['testUri'] = '/html-test-site/site/index.html'
	systemProperties['hubUrl'] = 'localhost'
	systemProperties['hubPort'] = '4444'
	
    testLogging {
        exceptionFormat "full"
        events "started", "passed", "skipped", "failed", "standardOut", "standardError"
        displayGranularity = 0
        showStandardStreams = false
    }
	
    beforeTest { descriptor ->
        logger.lifecycle("Running test: " + descriptor)
    }    
   
}

task runParallelTestsInFirefox(type: Test) {
    description = 'Runs all JUnit test classes in parallel threads.'
    include '**/TestHandleCache*.class'
    testReportDir = file("${reporting.baseDir}/ParallelTestsFF")
    testResultsDir = file("${buildDir}/test-results/ParallelTestsFF")
    
	// System properties passed to tests
	systemProperties['browserType'] = 'firefox'
	
    // initial browser size and position
    systemProperties['windowXPosition'] = '100'
    systemProperties['windowYPosition'] = '40'
    systemProperties['windowWidth'] = '400'
    systemProperties['windowHeight'] = '600'    
}

task runFrameTestInIE(type: Test) {
    description = 'Runs a test of handling iframes.'
    maxParallelForks = 1
    include '**/TestFrames.class'
    testReportDir = file("${reporting.baseDir}/FrameTestIE")
    testResultsDir = file("${buildDir}/test-results/FrameTestIE")
    
    // System properties passed to tests
    systemProperties['browserType'] = 'ie'
    
    // initial browser size and position
    systemProperties['windowXPosition'] = '100'
    systemProperties['windowYPosition'] = '40'
    systemProperties['windowWidth'] = '800'
    systemProperties['windowHeight'] = '600'    
    
}

